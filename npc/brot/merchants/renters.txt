//= otRag + Cronus r3552

// Criador de Peco Peco (para Cavaleiros)----------------------------
prontera,55,350,0	script	Criador de Pecopecos	105,{

	mes "[Criador de Pecopecos]";
	if (Class == Job_Knight || Class == Job_Knight_H)
	{
		mes "Bom dia honorável cavaleir"+(Sex?"o":"a")+", gostaria de comprar um Pecopeco?";
		mes "Você precisa ter a Habilidade Montaria e apenas 2500 zeny sobrando.";
		next;
		if (select ("Sim, por favor.:Não obrigad"+(Sex?"o":"a")+".") == 1)
		{
			if (Zeny < 2500)
			{
				mes "[Criador de Pecopecos]";
				mes "Honorável cavaleir"+(Sex?"o":"a")+", você não possui dinheiro o bastante.";
				mes "Eu te emprestaria um pouco se pudesse....";
				close;
			}
			set Zeny,Zeny - 2500;
			setriding;
			close;	
		}
		mes "[Criador de Pecopecos]";
		mes "É mesmo? Então tenha um bom dia.";
		close;
	}
	mes "Eu sinto muito.";
	mes "Estes Pecopecos são exclusivos para os cavaleiros.";
	close;
}

// Criador de Peco Peco (para Templários)----------------------------
prontera,232,318,3	script	Criador de Peco Peco#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[Criador de Peco Peco]";
	if (BaseJob == Job_Crusader) {
		if(Upper != 1 ) mes "Bem-vindo Templário.";
		else mes "Bem-vindo Paladino.";
		mes "Nós temos um tipo especial";
		mes "de Peco Peco preparado para";
		mes "você. Para alugar um";
		mes "custará "+.@price+" zenys.";
		next;
		switch(select("Alugar um Peco Peco:Sair")) {
		case 1:
			if (zeny < .@price) {
				mes "[Criador de Peco Peco]";
				mes "Você não possui a";
				mes "quantidade suficiente";
				mes "de zenys. Se você quer ter";
				mes "um Peco Peco traga";
				mes ""+.@price+" zenys por favor...";
				close;
			}
			else if(getskilllv(63) == 0) {
				mes "[Criador de Peco Peco]";
				mes "Você precisa primeiro aprender";
				mes "a andar em um Peco Peco, depois que fizer";
				mes "isto eu poderei alugar um para você.";
				close;
			}
			else if(checkriding()) {
				mes "[Criador de Peco Peco]";
				mes "Você já está";
				mes "montado em um Peco Peco.";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Criador de Peco Peco]";
			mes "Vejo você por aí.";
			close;		
		}
	} else {
		mes "O que posso fazer por você?";
		mes "Esteja ciente de que";
		mes "este seviço de aluguel";
		mes "de Peco Pecos é restrito a";
		mes "Templários e Paladinos.";
		close;
	}
}

// Criador de Falcões ---------------------------------------
in_hunter.gat,146,100,2	script	Treinador de falcões	59,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	//if(Upper==1) set .@price,2500; //Scarf Falcon
	//if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Treinador de falcões]";
	if (BaseJob == Job_Hunter) {
		mes "Você precisa de um falcão?";
		mes "Você pode alugar seu próprio";
		mes "falção por um preço justo de";
		mes "apenas "+.@price+" zenys~";
		next;
		switch(select("Alugar Falcão:Cancelar")) {
		case 1:
			if (zeny < .@price) {
				mes "[Treinador de falcões]";
				mes "O que é isso?";
				mes "Você não tem zenys";
				mes "o suficiente?!";
				mes "Você deveria começar";
				mes "a ganhar dinheiro";
				mes "matando monstros~";
				close;
			}
			else if(getskilllv(127) == 0) {
				mes "[Treinador de falcões]";
				mes "Vá aprender como";
				mes "cuidar de um Falcão";
				mes "primeiro! Eu não posso alugar";
				mes "um falcão para você se";
				mes "você não pode adestrá-lo...";
				close;
			}
			else if(checkfalcon()) {
				mes "[Treinador de falcões]";
				mes "Hum...";
				mes "Você já possui";
				mes "um Falcão. Está bem aí";
				mes "você não pode vê-lo?";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Treinador de falcões]";
			mes "E-espere, o que você";
			mes "está pensando? Estes";
			mes "Falcões são os melhores,";
			mes "Eu garanto isto! Vamos~";
			close;
		}
	} else {
		mes "Jovem Tolo!";
		mes "Falcões podem ser";
		mes "usados apenas por Caçadores";
		mes "e Atiradores de Elite, entendeu?";
		mes "...Heh heh, inveja?";
		close;
	}
}